apply plugin: 'com.android.application'

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent, "substratum_beta_" + versionCode + "_" + "${gitHash()}" + ".apk")
        }
    }

    defaultConfig {
        applicationId "projekt.substratum"
        minSdkVersion 24
        targetSdkVersion 25
        versionCode 601
        versionName "six hundred one"
        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
    }

    defaultConfig {
        jackOptions {
            enabled true
            additionalParameters('jack.incremental': 'true')
        }
    }

    compileOptions {
        incremental true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:support-v13:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:palette-v7:25.2.0'
    compile 'com.android.support:preference-v14:25.2.0'
    compile 'com.google.firebase:firebase-database:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    // Image Downloading and Caching
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    // Commons IO
    compile 'commons-io:commons-io:2.5'
    // ZipSigner
    compile files('src/main/libs/kellinwood-logging-android-1.4.jar')
    compile files('src/main/libs/kellinwood-logging-lib-1.1.jar')
    compile files('src/main/libs/kellinwood-logging-log4j-1.0.jar')
    compile files('src/main/libs/zipio-lib-1.8.jar')
    compile files('src/main/libs/zipsigner-lib-1.17.jar')
    // Loading
    compile 'me.zhanghai.android.materialprogressbar:library:1.1.6'
    // App Intro
    compile 'com.stephentuso:welcome:0.7.2'
    // Floating Action Buttons
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    // Material Drawer
    compile('com.mikepenz:materialdrawer:5.8.1@aar') {
        transitive = true
    }
    // About Libraries
    compile 'com.mikepenz:itemanimators:0.5.0@aar'
    compile('com.mikepenz:aboutlibraries:5.9.0@aar') {
        transitive = true
    }
    // Gesture RecyclerView
    compile 'com.thesurix.gesturerecycler:gesture-recycler:1.3.1'
    // Animated ImageView
    compile 'com.flaviofaria:kenburnsview:1.0.7'
    compile 'me.wangyuwei:GalleryView:1.0.0'
    // ZT Zip
    compile 'org.zeroturnaround:zt-zip:1.8'
    // Dialogs
    compile 'com.wang.avi:library:2.1.3'
    // Blur Effect
    compile 'com.eightbitlab:blurview:1.1.2'
    // Image Processing
    compile 'com.theartofdev.edmodo:android-image-cropper:2.3.1'
    // Direct Interface to the Overlay Manager Service
    compile files('src/main/libs/overlay-manager-service.jar')
    // Expandable Layout
    compile 'net.cachapa.expandablelayout:expandablelayout:2.6'
    // Floating App Head
    compile 'com.github.recruit-lifestyle:FloatingView:2.1'
}
apply plugin: 'com.google.gms.google-services'