/*
 * Copyright (c) 2016-2017 Projekt Substratum
 * This file is part of Substratum.
 *
 * Substratum is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Substratum is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Substratum.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "substratum_beta_${variant.versionCode}_${gitHash()}.apk"
        }
    }

    defaultConfig {
        applicationId "projekt.substratum"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 795
        versionName "seven hundred ninety five"
        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "preview"
    productFlavors {
        main {}
        preview {
            targetSdkVersion 26
            versionNameSuffix " (target-preview)"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Leak Checks and Test Cases
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'junit:junit:4.12'
    // Android Support Libraries
    compile 'com.android.support:appcompat-v7:26.0.0-beta2'
    compile 'com.android.support:cardview-v7:26.0.0-beta2'
    compile 'com.android.support:recyclerview-v7:26.0.0-beta2'
    compile 'com.android.support:support-v13:26.0.0-beta2'
    compile 'com.android.support:design:26.0.0-beta2'
    compile 'com.android.support:palette-v7:26.0.0-beta2'
    compile 'com.android.support:preference-v14:26.0.0-beta2'
    // Firebase
    compile 'com.google.firebase:firebase-core:11.0.1'
    compile 'com.google.firebase:firebase-crash:11.0.1'
    compile 'com.google.firebase:firebase-database:11.0.1'
    compile 'com.google.firebase:firebase-messaging:11.0.1'
    // Image Downloading and Caching
    compile 'com.github.bumptech.glide:glide:4.0.0-RC1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC0'
    // Commons IO
    compile 'commons-io:commons-io:2.5'
    // APK Signer
    compile 'com.android.tools.build:apksig:2.3.3'
    compile "org.bouncycastle:bcprov-jdk16:1.45"
    // Loading
    compile 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    // App Intro
    compile 'com.stephentuso:welcome:1.4.1'
    // Floating Action Buttons
    compile 'com.gordonwong:material-sheet-fab:1.2.1'
    // Material Drawer
    compile('com.mikepenz:materialdrawer:5.9.3@aar') {
        transitive = true
    }
    // About Libraries
    compile 'com.mikepenz:itemanimators:0.5.0@aar'
    compile('com.mikepenz:aboutlibraries:5.9.6@aar') {
        transitive = true
    }
    // Gesture RecyclerView
    compile 'com.thesurix.gesturerecycler:gesture-recycler:1.3.1'
    // Animated ImageView
    compile 'com.flaviofaria:kenburnsview:1.0.7'
    // ZT Zip
    //noinspection GradleDependency
    compile 'org.zeroturnaround:zt-zip:1.8'
    // Dialogs
    compile 'com.wang.avi:library:2.1.3'
    // Blur Effect
    compile 'com.eightbitlab:blurview:1.3.3'
    // Image Processing
    compile 'com.theartofdev.edmodo:android-image-cropper:2.4.2'
    // Direct Interface to the Overlay Manager Service
    compile files('src/main/libs/overlay-manager-service.jar')
    // Expandable Layout
    compile 'net.cachapa.expandablelayout:expandablelayout:2.9.1'
    // Floating App Head
    compile 'com.github.recruit-lifestyle:FloatingView:2.2'
    // Animations
    compile 'com.github.vlad1m1r990:Lemniscate:1.2.0'
    compile 'com.daimajia.numberprogressbar:library:1.4@aar'
}
apply plugin: 'com.google.gms.google-services'