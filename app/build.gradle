/*
 * Copyright (c) 2016-2017 Projekt Substratum
 * This file is part of Substratum.
 *
 * Substratum is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Substratum is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Substratum.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "substratum_${variant.versionCode}_${gitHash()}.apk"
        }
    }

    defaultConfig {
        applicationId "projekt.substratum"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 846
        versionName "eight hundred fourty six"
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""
    }

    // If the keystore file exists
    if (keystorePropertiesFile.exists()) {
        // Initialize a new Properties() object called keystoreProperties.
        def keystoreProperties = new Properties()

        // Load your keystore.properties file into the keystoreProperties object.
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) signingConfig signingConfigs.release
        }
    }

    flavorDimensions "preview"
    productFlavors {
        main {}
        preview {
            targetSdkVersion 26
            versionNameSuffix " (target-preview)"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

ext {
    leakCanaryVersion = '1.5.1'
    supportLibsVersion = '26.1.0'
    firebaseVersion = '11.2.2'
    glideVersion = '4.1.1'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Leak Checks and Test Cases
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation 'junit:junit:4.12'
    // Android Support Libraries
    implementation "com.android.support:appcompat-v7:$supportLibsVersion"
    implementation "com.android.support:exifinterface:$supportLibsVersion"
    implementation "com.android.support:cardview-v7:$supportLibsVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibsVersion"
    implementation "com.android.support:support-v13:$supportLibsVersion"
    implementation "com.android.support:design:$supportLibsVersion"
    implementation "com.android.support:palette-v7:$supportLibsVersion"
    implementation "com.android.support:preference-v14:$supportLibsVersion"
    // Firebase
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-crash:$firebaseVersion"
    implementation "com.google.firebase:firebase-database:$firebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    // Image Downloading and Caching
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"
    // Commons IO
    implementation 'commons-io:commons-io:2.5'
    // APK Signer
    implementation 'com.android.tools.build:apksig:2.5.0-alpha-preview-02'
    // Loading
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    // App Intro
    implementation 'com.stephentuso:welcome:1.4.1'
    // Floating Action Buttons
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    // Material Drawer
    implementation('com.mikepenz:materialdrawer:5.9.5@aar') {
        transitive = true
    }
    // About Libraries
    //implementation 'com.mikepenz:itemanimators:1.0.0@aar'
    implementation('com.mikepenz:aboutlibraries:5.9.7@aar') {
        transitive = true
    }
    // Gesture RecyclerView
    implementation 'com.thesurix.gesturerecycler:gesture-recycler:1.3.1'
    // Animated ImageView
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    // ZT Zip
    //noinspection GradleDependency
    implementation 'org.zeroturnaround:zt-zip:1.8'
    // Dialogs
    implementation 'com.wang.avi:library:2.1.3'
    // Image Processing
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.4.7'
    // Direct Interface to the Overlay Manager Service
    implementation files('src/main/libs/oms-v3.jar')
    // Expandable Layout
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.1'
    // Floating App Head
    implementation 'com.github.recruit-lifestyle:FloatingView:2.2.3-beta1'
    // Crash Activity
    implementation 'cat.ereza:customactivityoncrash:2.1.0'
}
apply plugin: 'com.google.gms.google-services'
